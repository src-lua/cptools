#compdef cptools cpt

_cptools() {
    local context state state_descr line
    typeset -A opt_args

    _arguments -C \
        '1: :->cmds' \
        '*:: :->args'

    case $state in
        cmds)
            local -a commands
            commands=(
                'new:Create a new contest'
                'add:Add a single file with template'
                'mark:Mark problem status (default: AC)'
                'status:Show contest progress summary'
                'open:Open problem URL in browser'
                'test:Compile and test a solution'
                'fetch:Fetch sample test cases'
                'stress:Stress test solution vs brute force'
                'clean:Remove compiled binaries'
                'update:Update info.md'
                'init:Initialize a new contest repo'
                'config:Edit configuration'
                'commit:Commit and push changes'
                'bundle:Bundle includes and copy to clipboard'
            )
            _describe 'command' commands
            ;;
        args)
            case $line[1] in
                mark)
                    _arguments \
                        '1:problem:_files -g "*.cpp(.:r)"' \
                        '2:status:(AC WA TLE MLE RE WIP ~)' \
                        '3:directory:_files -/'
                    ;;
                add|open)
                    _arguments \
                        '1:name:_files -g "*.cpp(.:r)"' \
                        '2:directory:_files -/'
                    ;;
                test)
                    _arguments \
                        '1:problem:_files -g "*.cpp(.:r)"' \
                        '2:directory:_files -/'
                    ;;
                fetch)
                    _arguments \
                        '1:problem:_files -g "*.cpp(.:r)"' \
                        '2:directory:_files -/'
                    ;;
                stress)
                    _arguments \
                        '1:solution:_files -g "*.cpp(.:r)"' \
                        '2:brute:_files -g "*.cpp(.:r)"' \
                        '3:generator:_files -g "*.cpp(.:r)"' \
                        '--checker[Custom checker]:checker:_files -g "*.cpp(.:r)"'
                    ;;
                clean|update)
                    _arguments \
                        '--all[Apply to all platform directories]' \
                        '*:directory:_files -/'
                    ;;
                new)
                    _arguments \
                        '1:url:'
                    ;;
                status)
                    _arguments \
                        '1:directory:_files -/'
                    ;;
                init)
                    _arguments \
                        '--nogit[Skip git init]' \
                        '1:directory:_files -/'
                    ;;
                commit)
                    _arguments \
                        '--all[Commit all contest directories]' \
                        '*:directory:_files -/'
                    ;;
                bundle)
                    _arguments \
                        '1:problem:_files -g "*.cpp(.:r)"' \
                        '-o[Output file]:file:_files -g "*.cpp"' \
                        '-i[Bundle in-place]'
                    ;;
            esac
            ;;
    esac
}

_cptools "$@"
